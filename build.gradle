buildscript {
    ext.javaRepos = {
        mavenCentral()
        jcenter()
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'com.palantir.docker' version '0.25.0'
}


repositories javaRepos

apply plugin: 'java'

javadoc.failOnError = false

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

version = 1.0

apply plugin: 'maven-publish'

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
    testImplementation "org.assertj:assertj-core:$assertJVersion"
    testImplementation "org.apache.lucene:lucene-test-framework:$luceneVersion"
    testImplementation "org.apache.solr:solr-test-framework:$luceneVersion"
    testRuntimeOnly(
            "org.junit.jupiter:junit-jupiter-engine:$junitVersion",
            "org.junit.vintage:junit-vintage-engine:$junitVersion"
    )

    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion"
    implementation "org.slf4j:slf4j-api:$slf4jVersion"
    implementation "org.slf4j:slf4j-ext:$slf4jVersion"
    compileOnly "org.apache.solr:solr-core:$luceneVersion"
    compileOnly "org.apache.lucene:lucene-core:$luceneVersion"
    compileOnly "org.apache.lucene:lucene-analyzers-common:$luceneVersion"
}


sourceSets.test {
    resources.srcDirs = ["src/test/resources"]
}


test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    systemProperty "test.solr.allowed.securerandom", "NativePRNG"
}


docker {
    name 'queryanalysis/solr:8'
    dockerfile file('docker/solr/Dockerfile')
    files shadowJar.outputs, 'docker/solr'

}

docker.dependsOn(shadowJar)
